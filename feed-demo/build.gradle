plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {

    def versionPropsFile = file('version.properties')  //maintains version number of apk
    Integer versionNumber = 10000

    if (versionPropsFile.exists()) {                  //create if does not exist
        def value = 0

        Properties versionProps = new Properties()
        def runTasks = gradle.startParameter.taskNames
        if ('assembleRelease' in runTasks) {  //check if release build is getting executed
            value = 1
        }

        if (versionPropsFile.canRead()) {
            versionProps.load(new FileInputStream(versionPropsFile))

            //increase build number every time build happens
            //increase version number and patch number only in case of release build
            versionProps['VERSION_NUMBER'] = (versionProps['VERSION_NUMBER'].toInteger() + value).toString()
            versionNumber = versionProps['VERSION_NUMBER'].toInteger()
            versionProps.store(versionPropsFile.newWriter(), null)
        }
    }

    defaultConfig {
        applicationId "com.rizzle.sdk.faas.demo"
        minSdk 21
        targetSdk 33
        compileSdk 33

        project.logger.lifecycle('Generationg build with version number --->')
        project.logger.lifecycle("${versionNumber.toInteger()}")

        versionCode versionNumber.toInteger()
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        setProperty("archivesBaseName", "FaaS_${versionName}_${versionCode}")
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".dev"
        }
        qa {
            initWith debug
            applicationIdSuffix ".qa"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            matchingFallbacks = ['debug']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    // Glide
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
    implementation project(path: ':feed-sdk')

    //    implementation ('com.rizzle.sdk:feed:0.0.1-SNAPSHOT')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    //timber
    implementation 'com.jakewharton.timber:timber:5.0.1'
}